#---- cesrdef ----
#
# Define cesr specific environment variables;
# to be used by csh-like shells: csh,tcsh.
# (Sh, ksh, bash should use cesrdefs)
#
# Shamelessly stolen from the CLEO cleodef script 
# M. Palmer
#
# Usage in .login:
#   source /home/cesrulib/bin/cesrdef
# or switch to other versions of libraries and constants: 
#   setenv CESRLIB devel; source /home/cesrulib/bin/cesrdef
# or define aliases in .cshrc, e.g.:
#   alias cesrdef "source /home/cesrulib/bin/cesrdef" 
#   alias current "setenv CESRLIB current ; source /home/cesrulib/bin/cesrdef"
#   alias devel   "setenv CESRLIB devel ; source /home/cesrulib/bin/cesrdef"
#-------------------------------------
#
# Specify CESR Librarian bin area
#
if ( $?CESRLIB_BASE == 0 ) set CESRLIB_BASE=/home/cesrulib
if ( $?CESRLIB_BIN  == 0 ) set CESRLIB_BIN=${CESRLIB_BASE}/bin

#
# Get platform/OS tags
#
set PLATFORM_TAG="`uname -s`_`uname -p`"

#
#	Switchable version
#
if ( $?CESRLIB    == 0 ) setenv CESRLIB     current
if ( $?CERN_LEVEL == 0 ) setenv CERN_LEVEL  pro

#
#	Root names
#
if ( $?CONST    == 0 ) setenv CONST     /home/cesrulib/constants
if ( $?CESR_    == 0 ) setenv CESR_     ${CESR_BASE}/cesr_libs
if ( $?CERN     == 0 ) setenv CERN      /nfs/cern

#
# Source the CESR environment script
#
if ( $?CESRENV    == 0 ) setenv CESRENV /home/cesrulib/bin/cesrenv
if ( -f $CESRENV )  then
	source $CESRENV
else
	echo File $CESRENV not found.
endif

#
#-----  Add CESR Librarian bin area to user's PATH if not already present ----
#
set PATH_LIST=`echo ${PATH} | sed s/:/\ /g`
set found=0
foreach dir (${PATH_LIST})
  if ( "${dir}" == "${CESRLIB_BIN}" ) set found=1
end
if ( $found == 0 ) setenv PATH ${PATH}:${CESRLIB_BIN}


#
# $Id$
#
# $Log$
# Revision 1.5  2003/09/08 15:14:47  cesrulib
# Updates for automatic platform dependence and synchronized release/packages
# areas.
#
# Revision 1.4  2003/05/27 15:18:19  palmer
# Add CERN variable definitions.
#
# Revision 1.3  2003/05/21 12:33:17  palmer
# Add PKG_INC environment variable.
# Fix miscellaneous comments.
# Automatically append /home/cesrulib/bin to user's PATH environment variable.
#
# Revision 1.2  2001/10/04 20:35:21  palmer
# Add environment variables for paths to the external packages area.
#
# Revision 1.1  2001/09/29 18:18:14  palmer
# Environment variable utilities for CESR libraries on Unix.
#
#
