#---- cesrdef ----
#
# Define cesr specific environment variables;
# to be used by csh-like shells: csh,tcsh.
# (Sh, ksh, bash should use cesrdefs)
#
# Shamelessly stolen from the CLEO cleodef script 
# M. Palmer
#
# Usage in .login:
#   source /home/cesrulib/bin/cesrdef
# or switch to other versions of libraries and constants: 
#   setenv CESRLIB devel; source /home/cesrulib/bin/cesrdef
# or define aliases in .cshrc, e.g.:
#   alias cesrdef "source /home/cesrulib/bin/cesrdef" 
#   alias current "setenv CESRLIB current ; source /home/cesrulib/bin/cesrdef"
#   alias devel   "setenv CESRLIB devel ; source /home/cesrulib/bin/cesrdef"
#-------------------------------------
#
# Get platform/OS tags
#
set PLATFORM_TAG="`uname -s | cut -d- -f1`_`uname -p`"

#
# Specify CESR Librarian base areas
# Presently have Unix/Linux and Cygwin versions
#
set BASE_DIR_DEF=/home/cesrulib
if ( "${PLATFORM_TAG}" == "CYGWIN_NT_i686" ) then 
  set BASE_DIR_DEF=//lns101/cesrulib
endif

if ( $?CESRLIB_BASE == 0 ) set CESRLIB_BASE=${BASE_DIR_DEF}
if ( $?CESRLIB_BIN  == 0 ) set CESRLIB_BIN=${CESRLIB_BASE}/bin

#
#	Switchable version
#
if ( $?CESRLIB    == 0 ) setenv CESRLIB     current
if ( $?CERN_LEVEL == 0 ) setenv CERN_LEVEL  pro

#
#	Root names
#
if ( $?CONST    == 0 ) setenv CONST     ${CESRLIB_BASE}/constants
if ( $?CESR_    == 0 ) setenv CESR_     ${CESRLIB_BASE}/cesr_libs
if ( $?CERN     == 0 ) setenv CERN      /nfs/cern

#
# Source the CESR environment script#
# Now clear all override variables that, by default, use a 
# derivative path from $CESRLIB_BASE.  This does NOT need 
# to be done for the "version" variables
#
#
if ( $?CESRENV  == 0 ) setenv CESRENV ${CESRLIB_BIN}/cesrenv
if ( -f "$CESRENV" )  then
	source $CESRENV
else
	echo File $CESRENV not found.
endif

#
#-----  Add CESR Librarian bin area to user's PATH if not already present ----
#
set PATH_LIST=`echo ${PATH} | sed s/:/\ /g`
set found=0
foreach dir (${PATH_LIST})
  if ( "${dir}" == "${CESRLIB_BIN}" ) set found=1
end
if ( $found == 0 ) setenv PATH ${PATH}:${CESRLIB_BIN}

#
# Add hook to support OLD style of environment variables
# Clear variable when done
#
if ( $?CESR_ENV_OLD == 1 ) setenv CESR_LIB ${CESR_REL}
unset    CESR_ENV_OLD
unsetenv CESR_ENV_OLD

#
# Now clear all override environment variables that, by default, use a 
# derivative path from $CESRLIB_BASE.  This does NOT need to be done 
# for the "version" variables.  ALSO, clear all shell variables with 
# names corresponding to the override environment variables to be safe 
# (NOTE:  for variable substitutions in csh-like shells, shell variable 
# assignments take precedence over environment variable assignments)
#
unset    CESRLIB_BASE
unset    CESRLIB_BIN
unset    CESRLIB
unset    CERN_LEVEL
unset    CONST
unset    CESR_
unset    CERN
unset    CESRENV

unsetenv CESRLIB_BIN
unsetenv CONST
unsetenv CESR_
unsetenv CESRENV

#
# $Id$
#
# $Log$
# Revision 1.7  2004/11/16 16:01:37  palmer
# Update environment variable structure including support for Windows and
# new naming conventions.
#
# Revision 1.6  2003/09/08 18:06:44  cesrulib
# Bug fix - variable name typo.
#
# Revision 1.5  2003/09/08 15:14:47  cesrulib
# Updates for automatic platform dependence and synchronized release/packages
# areas.
#
# Revision 1.4  2003/05/27 15:18:19  palmer
# Add CERN variable definitions.
#
# Revision 1.3  2003/05/21 12:33:17  palmer
# Add PKG_INC environment variable.
# Fix miscellaneous comments.
# Automatically append /home/cesrulib/bin to user's PATH environment variable.
#
# Revision 1.2  2001/10/04 20:35:21  palmer
# Add environment variables for paths to the external packages area.
#
# Revision 1.1  2001/09/29 18:18:14  palmer
# Environment variable utilities for CESR libraries on Unix.
#
#
