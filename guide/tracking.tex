\chapter{Tracking}
\label{c:tracking}
\index{Tracking}

\bmad can do two types of tracking. One type uses a single particle
and tracks its coordinates throughout the lattice. The other type
takes a beam distribution and tracks the centroids and sigmas of
``macroparticles''. Discussion of macroparicle tracking will be
deferred until the last section of this chapter.

The routine \vnr{track1} is the routine that tracks through one
element in the lattice. The routine \vnr{track_all} calls \vn{track1}
in a loop over all elements to track through the entire
lattice. Alternatively the routine \vnr{track_many} can be used to
track through a selective number of elements or to track backwards
(See Section~\ref{s:reverse_track}).

%----------------------------------------------------------------
\section{Track\_All Example}
\index{Tracking!example}

\index{Param_struct!lost}
\index{Param_struct!ix_lost}
The routines to track through a lattice are listed in
Section~\ref{r:track}. The \vn{track_all} routine serves as a good
example. \vn{track_all} tracks a particle through a lattice from
beginning to end. Its code, condensed slightly, is shown below.
\begin{example}
  subroutine track_all (lattice, orbit)
    use bmad_struct
    use bmad_interface
    implicit none
    type (ring_struct)  lattice
    type (coord_struct), allocatable :: orbit(:)
    integer n

  ! Init

    lattice%param%lost = .false.
    if (size(orbit) < lattice%n_ele_max+1) &
                    call reallocate_coord (orbit, lattice%n_ele_max)

  ! Track through the elements and check for lost particles.

    do n = 1, lattice%n_ele_use
      call track1 (orbit(n-1), lattice%ele_(n), lattice%param, orbit(n))
      if (lattice%param%lost) then
        lattice%param%ix_lost = n
        return
      endif
    enddo
  end subroutine
\end{example}
The \vn{reallocate_coord} call is done in case the number of elements
in the lattice has changed. \vn{orbit(n)} holds the particle's
position at the exit end of the $n$\Th element. The call to
\vn{track1} tracks through one element from the exit end of the
$n-1$\St\ element to the exit end of the $n$\Th
particle. \vn{lattice%param%lost} is a logical that signals the
calling routine whether a particle has been lost.  This generally
happens when the particle's position is larger then the aperture. When
a particle is lost \vn{lattice%param%ix_lost} is used to record in
what element the loss occured.

%----------------------------------------------------------------
\section{Apertures}
\index{Tracking!apertures}

\index{Param_struct!aperture_limit_on}
The logical \vn{lattice%param%aperture_limit_on} determines if
element apertures given by \vn{lattice%ele_(:)%value(x_limit\$)} and
\vn{lattice%ele_(:)%value(y_limit\$)} (See
Section~\ref{s:limit}) are used to determine if a particle has been
lost in tracking. A value of zero for either of these limits means
that the aperture is infinite in that plane.  The default is for
\vn{lattice%param%aperture_limit_on} to be True.  Even if
\vn{lattice%param%aperture_limit_on} is False there is a ``hard''
aperture limit set by \vn{bmad_com%max_aperture_limit}.  This hard
limit is used to prevent floating point overflows. The default hard
aperture limit is 1000 meters. Additionally, even if a particle is
within the hard limit, some routines will mark a particle as lost if
the tracking calculation will result in an overflow.

\index{Param_struct!end_lost_at}
\index{Param_struct!lost}
\index{Param_struct!ix_lost}
\index{Element !entrance end}
\index{Element !exit end}
\vn{lattice%param%lost} is the logical to check to see if a particle has
been lost. \vn{lattice%param%ix_lost} gives the index of the element
at which a particle is lost and \vn{%param%end_lost_at} gives which
end the particle was lost at. The possible values for
\vn{lattice%param%end_lost_at} are:
\begin{example}
  entrance_end$
  exit_end$
\end{example}
When tracking forward, if a particle is lost at the exit end of an
element then the place where the orbit was outside the aperture is at
\vn{orbit(ix)} where \vn{ix} is the index of the element where the
particle is lost (given by \vn{lattice%param%ix_lost}). If the
particle is lost at the entrance end then the appropriate index is one
less (remember that \vn{orbit(i)} is the orbit at the exit end of an
element). To sort this out and to determine in what plane the particle
is lost in use the routine \vnr{lost_particle_info}.

%----------------------------------------------------------------
\section{The coord\_struct}
\index{coord_struct}

For single particle tracking the starting point is the
\vn{coord_struct} whose definition is 
\begin{example}
  type coord_struct
    real(rp) vec(6)   ! (x, p_x, y, p_y, z, p_z)
  end type
\end{example}
shown
The \vn{coord_struct} defines the phase
space vector of the particle at a certain longitudinal location.

To get an orbit, that is, the particle position at every element in a
lattice, you will need an array of \vn{coord_struct}'s. Since the
number of elements in the lattice is not known in advance the array
must be declared to be allocatable.
\begin{verbatim}
  type (coord_struct), allocatable :: coords(:)
\end{verbatim}
You cannot, in general, use a pointer to an array since most \bmad
subroutines expect an allocatable array and Fortran does not allow you
to mix and match. An example of how to do multi-turn tracking is
\begin{example}
  type (ring_struct) lattice             ! lattice to track through
  type (coord_struct), allocatable :: coord(:)
  ...
  call bmad_parser ('this_lattice', lattice)
  ...
  call reallocate_coords (coord, lattice%n_ele_max)
  coords(0)%vec = (/ 0.01, 0.2, 0.3, 0.4, 0.0, 0.0 /) ! initialization
  do i = 1, n_turns
    call track_all (lattice, coord)
    coords(0) = coords(lattice%n_ele_use)
  end do
\end{example}
\vn{track_all} takes \vn{coord(0)} and tracks through the lattice from
the 1\St\ element through the $n$\Th\ where $n =$
\vn{lattice%n_ele_use}.  Each \vn{coord(i)} is associated with the
corresponding \vn{lattice%ele_(i)} element and is the particle position
at the exit end of the i\Th\ element. \vn{reallocate_coords} simply
does an allocation. Its advantage over just allocating directly is
that it checks if the allocation is needed and if not then it does
nothing. Since \vn{track_all}, like many \bmad routines, will
reallocate if needed, only one call to \vn{reallocate_coords} is
needed initially even if there are repeated calls to \vn{track_all}.

If you are writing a routine where the \vn{coord_struct} array is
local (not passed as an argument to the routine) then you have to
decide how to cleanup the allocated \vn{coord_struct} memory at the
end of the routine. In general you have two choices: 1) Deallocate
the array. This is the cleanest solution but it can be slow since you
have to allocate afresh each time the routine is called. 2) Use the
save attribute so that the array stays around until the next time the
routine is called 
\begin{example}
  type (coord_struct), allocatable, save :: orb(:) 
\end{example}
Saving the \vn{coord_stuct} is faster but leaves memory tied up. 

%----------------------------------------------------------------
\section {Miscellaneous Notes}

\index{Ele_struct!%mat6}
Except for \vntm{Linear} tracking, none of the \bmad tracking routines
make use of the \vn{ele%mat6} transfer matrix. The reverse, however,
is not true.  The transfer matrix routines (\vn{lattice_make_mat6}, etc.)
will do tracking.

\index{Ele_struct!%tracking_method}
For each element the method of tracking may be set either via the
input lattice file (see section~\ref{s:tkm}) or directly in the
program by setting the \vn{%tracking_method} attribute of an element
\begin{verbatim}
  type (ele_struct) ele
  ...
  ele%tracking_method = boris$  ! for boris tracking
\end{verbatim}
To form the corresponding parameter to a given tracking method just
put ``\$'' after the name. For example, the \vn{bmad_standard}
tracking method is specified by the \vn{bmad_standard\$}
parameter.

\index{Synchrotron radiation!calculating}
\bmad simulates radiation damping and excitation by applying a kick
just before and after each element. To turn on radiation damping
and/or excitation use the \vnr{setup_radiation_tracking} routine.

%----------------------------------------------------------------
\section{Macroparticle Tracking}
\index{Macroparticles!tracking}
\index{Tracking!Macroparticles}

The definition of the \vn{macro_struct} structure that defines a
macroparticle is shown in Figure~\ref{f:macro_struct}.  
\begin{figure}[htb]
\centering
\small
\begin{verbatim}
  type macro_struct
    type (coord_struct) r   ! Center of the macroparticle
    real(rp) sigma(21)      ! Sigma matrix.
    real(rp) :: sig_z = 0   ! longitudinal macroparticle length.
    real(rp) k_loss         ! loss factor (V/m). scratch variable for tracking.
    real(rp) charge         ! charge in a macroparticle (Coul).
    integer :: iz = 0       ! index to ordering of particles in z.
  end type
\end{verbatim}
\caption{The \vn{macro\_struct}\ that defines a macroparticle.}
\label{f:macro_struct}
\end{figure}

\vn{%r} is the
coordinates of the macroparticle center. \vn{%sigma(:)} holds the
sigma matrix. Since the $6 \times 6$ sigma matrix is symmetric only 21
elements need to be stored. Symbolic constants \vn{s$ij$\$} ($i$, $j$
= $1, \ldots, 6$) are defined to map to between the linear
\vn{%sigma(:)} array and the matrix. Example
\begin{example}
  type (macro_struct) macro
  sigma_z = sqrt(macro%sigma(s55\$))
\end{example}
To convert \vn{%sigma(:)} into an actual matrix use the
\vn{mp_sigma_to_mat} routine.

\index{LIAR}
\bmad uses canonical phase space
coordinates(See Section~\ref{s:phase_space_coords}). The LIAR program uses
``angle'' phase space coordinates
\begin{equation}
  (x, x', y, y', -z, E)
\end{equation}
The notation can be confusing here since in LIAR's notation positive
$z$ is in the opposite direction to the \bmad convention. Thus in
LIAR's notation the LIAR phase space coordinates are
\begin{equation}
  (x, x', y, y', z, E)
\end{equation}
To convert between \bmad and LIAR coordinates there are two routines
called \vn{mp_to_angle_coords} and \vn{mp_to_canonical_coords}.

macroparticles are grouped into slices, a number of slices make up a
bunch, and a number of bunches make up a beam. Initialization of a
\vn{beam_struct} structure can be done via the
\vn{init_macro_distribution} which implements the LIAR initialization
algorithm (See Section~\ref{s:macro}). Once a beam is initialized,
tracking can be performed using \vn{track_beam}. An example program is
shown in figure~\ref{f:macro_program}. 

the \vn{init} variable (line 11) holds the information to initialize
the macroparticle distribution. The values of the components in the
\vn{init} variable are set using a namelist read (line 24). An example
of an input file that can be used to set \vn{init} is shown in
figure~\ref{f:beam_init}. Line 23 sets the initial reference energy of
the beam to be the reference energy at the start of the linac so this
does not have to be set in the input file.  The
\vn{init_macro_distribution} routine (line 27) initializes the
\vn{beam} variable. The \vn{beam} variable is a container that holds
the macroparticles that make up the beam. The \vn{track_beam} routine
tracks these macroparticles through to the end of the linac. If it is
desired to track only through part of the linac then \vn{track_beam}
has optional arguments that allow this. lines 30 through 41 print out
some horizontal parameters from the position at the end of the linac.


\begin{figure}
\begin{listing}{1}
program macroparticle_test

  use bmad
  use macroparticle_mod

  implicit none

  type (ring_struct) lattice
  type (beam_struct) beam
  type (macroparticle_struct) mp
  type (macro_init_struct) init

  integer i, j, k, n

  namelist / beam_init / init

!

  bmad_com%use_liar_lcavity = .true.
  call bmad_parser ('test.bmad', lattice)

  open (1, file = 'test.init', status = 'old')
  init%E_0 = lattice%ele_(0)%value(beam_energy$)
  read (1, nml = beam_init)
  close (1)

  call init_macro_distribution (beam, init, .true.)
  call track_beam (lattice, beam)

  print *
  print '(13x, a)', "x          x'    sig(1,1)    sig(1,2)    sig(2,2)"
  do i = 1, size(beam%bunch)
    do j = 1, size(beam%bunch(i)%slice)
      do k = 1, size(beam%bunch(i)%slice(j)%macro)
        mp = beam%bunch(i)%slice(j)%macro(k)
        call mp_to_angle_coords (mp, lattice%ele_(n)%value(beam_energy$))
        print '(3i2, 1p, 5e12.4)', i, j, k, mp%r%vec(1), mp%r%vec(2), &
                  mp%sigma(s11$), mp%sigma(s12$), mp%sigma(s22$)
      enddo
    enddo
  enddo

end program
\end{listing}
\caption{Example program showing macroparticle tracking.}
\label{f:macro_program}
\end{figure}

\begin{figure}
\begin{listing}{1}
\&beam_init
  init%x%beta = 4
  init%x%alpha = 0.16
  init%x%emit = 4.d-6
  init%y%beta = 16
  init%y%alpha = 4
  init%y%emit = 1.0d-8
  init%center = -5e-3, 1.0e-4, 1.0e-3, 0e-6, 0.0, 0.0
  init%sig_e = 10e-3
  init%sig_z = 10e-6
  init%sig_e_cut = 3
  init%sig_z_cut = 3
  init%n_bunch = 1
  init%n_slice = 2
  init%n_macro = 1
  init%n_part = 1e12
/
\end{listing}
\caption{Example beam initialization file.}
\label{f:beam_init}
\end{figure}

%----------------------------------------------------------------
\section{Reverse Tracking}
\label{s:reverse_track}
\index{Tracking!reverse}

There are two ways to do reverse tracking in which the particle goes
in the direction of decreasing \vn{s}. The first way is to use the
\vnr{track_many} routine. See the \vn{track_many} routine for more
details. The advantage of using \vn{track_many} is that it is
simple. The disadvantage is that it can slow things down some since
each element goes through a reversal process every time it is tracked
through. If a program is doing a lot of tracking the other option
is to form a reversed lattice with the elements in the reverse order
and track through that. The routine \vnr{ring_reverse} will do
this. One must be somewhat careful since the reversed lattice uses a
reversed coordinate system. The transformation between the reversed
and unreversed lattices is
\Begineq
  (x, p_x, y, p_y, z, p_z) -> (x, -p_x, y, -p_y, -z, p_z)
\Endeq
See the \vn{ring_reverse} routine for more details.
