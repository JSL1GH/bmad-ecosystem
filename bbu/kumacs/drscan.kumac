macro drscan jobid=0
*
* Make plots of bbu current thresholds
* obtained from tracking and analytic approximation
*
* jac 16 April 2009
*
v/del *
h/del *
pic/del *
*
exec ~/.pawlogon
opt zfl
opt nstat
opt ndat
set gsiz 0.36
set ygti 1.0
set mtyp 20
set mscf 0.24
set hcol 2
set pmci 2
set plci 2
set lwid 5.
set vsiz 0.3
set xlab 1.0
set ylab 1.2
*
if [jobid]=0 then
  fname='../drscan.out'
else
 if [jobid]<17 then
  fname='../jobs/job'//[jobid]//'/prstab2004.out'
 else
  fname='../jobs/job'//[jobid]//'/drscan.out'
 endif
endif
*
if $fexist([fname])=0 then
 mess Macro DRSCAN cannot find file [fname], quitting
 goto QUIT
endif
* 
vec/cr dr(100) r    100*0.
vec/cr curr1(100) r 100*0.
vec/cr curr2(100) r 100*0.
*
vec/read dr,curr1,curr2 [fname]
*
len=$vlen(dr,1)
*
if [len]=0 then
 mess No data found in file [fname], quitting
 goto QUIT
endif
*
* Make two plots, one log, one linear
do i=1,2
*
if [i]=1 then
 opt logy
 set xval 0.4
else
 sigma curr1=log(curr1)
 sigma curr2=log(curr2)
 opt liny
 set xval 0.2
endif
*
*title_g 'BBU Threshold Current: Comparison of Analytic Approximation to BMAD Tracking'
*title_g 'BBU Threshold Current - Job '//[jobid]//' - CERL7.4 Single HOM in One Cavity'
title_g 'BBU Threshold Current - Job '//[jobid]


v/copy dr(1:[len]) temp
v/del dr
v/copy temp dr
v/del temp
v/copy curr1(1:[len]) temp
v/del curr1
v/copy temp curr1
v/del temp
v/copy curr2(1:[len]) temp
v/del curr2
v/copy temp curr2


* Get histo limits
imi=$sigma(lvmin(dr))
xmi=dr([imi])
ima=$sigma(lvmax(dr))
xma=dr([ima])

imi=$sigma(lvmin(curr1))
ymi1=curr1([imi])
ima=$sigma(lvmax(curr1))
yma1=curr1([ima])
imi=$sigma(lvmin(curr2))
ymi2=curr2([imi])
ima=$sigma(lvmax(curr2))
yma2=curr2([ima])

ymi=[ymi1]
if [ymi2]<[ymi1] then
 ymi=[ymi2]
endif
yma=[yma1]
if [yma2]>[yma1] then
 yma=[yma2]
endif

xmin = 1.05*[xmi] - 0.05*[xma]
xmax = 1.05*[xma] - 0.05*[xmi]
ymin = 1.05*[ymi] - 0.05*[yma]
ymax = 1.05*[yma] - 0.05*[ymi]

mess [xmin] [xmax] [ymin] [ymax]
null [xmin] [xmax] [ymin] [ymax]

set plci 2
set pmci 2
set mscf 1.2
v/pl curr1%dr ! s
set plci 4
set pmci 4
set mscf 0.2
v/pl curr2%dr ! sl
set asiz 0.4

if [i]=1 then
 atit 'Ratio of Revolution Time to Bunch Spacing' 'I?th! (A)' ' ' 222
else
 atit 'Ratio of Revolution Time to Bunch Spacing' 'ln ( I?th! (A) )' ' ' 222
endif
*
selnt 1
set pmci 2
set ksiz 0.25
set csiz 0.28
key 2.9 17. 20 'Analytic Approximation' 
key 2.9 16. 1 'BMAD Tracking' 0.4 L
*
set chhe 0.2
set txfp -30
set txci 1

*itx 2.7 17 'See Beam-breakup instability theory for energy recovery linacs'
*itx 2.7 16.5 'G.H.Hoffstaetter and I.Bazarov, PRSTAB 7, 054401 (2004)'
*itx 2.7 16 'Fig. 3'
itx 2.65 18.15 'Beam-breakup instability theory for energy recovery linacs,'
itx 10.2 18.15 'G.H.Hoffstaetter and I.Bazarov, PRSTAB 7, 054401 (2004)'
*
enddo
*
psname = 'drscan_'//[jobid]//'.ps'
exec ~/.pawlogon#makeps file=[psname] type=-111
*
QUIT:
*
return
