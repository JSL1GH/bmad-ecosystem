\chapter{The lat_struct}
\label{c:lat_struct}
\index{Lat_struct|textbf}

The \vn{lat_struct} is the structure that holds of all the information 
about a lattice.   Despite its name, \bmad\
makes no assumption about whether an \vn{lat_struct} is circular as
with a storage ring or open as with a LINAC.

%----------------------------------------------------------------------------
\section{Elements Within the lat_struct}
\label {s:lat_struct}

\begin{figure}[htb]
\centering
\begin{verbatim}
type lat_struct
  type (mode_info_struct)  a, b, z  ! tunes, etc.
  character*16 name            ! Name in USE statement
  character*40 lattice         ! Lattice name
  character*80 input_file_name ! Lattice input file name
  character*80 title           ! From TITLE statement
  type (lat_param_struct) param    ! parameters
  type (ele_struct), pointer :: ele(:)    ! Array of lattice elements
  type (ele_struct)  ele_init              ! For use by any program
  type (control_struct)  control(n_control_maxx)  ! control list
  type (coord_struct) beam_start  ! Starting coordinates.
  integer version                  ! Version number for digested files
  integer n_ele_use                ! number of physical lattice elements
  integer n_ele_max                ! Index of last element used
  integer n_control_array          ! last index used in CONTROL_ array
  integer n_ic_array               ! last index used in IC_ array
  integer input_taylor_order       ! As set in the input file
  integer ic(n_control_maxx)      ! index to %control(:)
  real(rp), pointer :: E_tot ! = %ele(0)%value(E_tot$)
end type
\end{verbatim}
\caption{Definition of the \vn{lat_struct}.}
\label{f:lat_struct}
\end{figure}

\index{Lat_struct!%ele(:)}
\index{Lat_struct!%n_ele_track}
\index{Lat_struct!%n_ele_max}
\index{Ele_struct!in lat_struct} 
\index{Girder}
\index{Overlay}
\index{Group}
The definition of the \vn{lat_struct} is shown in
Figure~\ref{f:lat_struct}. The array \vn{%ele(:)} holds the elements
of the lattice. This array is always allocated with zero as the lower
bound.  \vn{%ele(0)} is a marker element with the name
\vn{BEGINNING}.  \vn{%ele(0)%mat6} is always the unit
matrix. \vn{%ele(0:)} is divided up into two parts: A ``physical''
part (also called the ``tracking'' part) and a ``control'' part (also
called the ``lord'' part). The physical part of this array holds the
elements that are tracked through. The physical part has \vn{free},
\vn{overlay_slave}, and \vn{multipass_slave} \vn{control_type} elements. The
control part holds the \vn{Overlay_lord}, \vn{Girder_lord},
\vn{multipass_lord}, \vn{super_lord}, and \vn{Group_lord}
\vn{control_type} elements (\sref{s:control_type}).  The bounds of
these two parts is given in Table~\ref{tab:part_extent}.
\begin{table}[htb]
\begin{center}
\begin{tabular}{|l|l|l|}
\hline
              & \multicolumn{2}{c|} {\em index n}         \\ \hline
{\em section} & {\em min}          & {\em max}            \\ \hline
physical      & 0                  & \vn{%n_ele_track}     \\ \hline
control       & \vn{%n_ele_track}+1 & \vn{%n_ele_max}      \\ \hline
\end{tabular} 
\caption{Bounds of the tracking and control parts 
of the array \vn{\%ele(:)}.}
\end{center}
\label{tab:part_extent}
\end{table}

\index{Lat_struct!%ele_init}
The \vn{%ele_init} component within the \vn{lat_struct} is not used
by \bmad\ and is available for general program use.

%----------------------------------------------------------------------------
\section{Param_struct Component}
\index{lat_param_struct}

The \vn{%param} component within the \vn{lat_struct} is a
\vn{lat_param_struct} structure whose definition is shown in
Figure~\ref{f:lat_param_struct}
\begin{figure}[htb]
\centering
\begin{verbatim}
  type lat_param_struct
    real(rp) n_part             ! Particles/bunch (for BeamBeam elements).
    real(rp) total_length       ! total_length of lattice
    real(rp) growth_rate        ! growth rate/turn if not stable
    real(rp) t1_with_RF(6,6)    ! Full 1-turn 6x6 matrix
    real(rp) t1_no_RF(6,6)      ! Transverse 1-turn 4x4 matrix (RF off).
    integer particle            ! +1 = positrons, -1 = electrons
    integer ix_lost             ! If lost at what element?
    integer end_lost_at         ! entrance_end$ or exit_end$
    integer lattice_type        ! linear_lattice$, circular_lattice$, etc...
    integer ixx                 ! Integer for general use
    logical stable              ! is closed lattice stable?
    logical aperture_limit_on   ! use apertures in tracking?
    logical lost                ! for use in tracking
  end type
\end{verbatim}
\caption{Definition of the \vn{param_struct}.}
\label{f:lat_param_struct}
\end{figure}

\index{lat_param_struct!%total_length}
\vn{%param%total_length} is the length of the lattice that a beam
tracks through
\begin{example}
  %param%total_length = %ele(n_use)%s - %ele(0)%s
\end{example}
Normally \vn{%ele(0)%s} = 0 so \vn{%param%total_length} =
\vn{%ele(n_use)%s} but this is not always the case.

\index{lat_param_struct!%n_part}
\index{BeamBeam}
\index{Lcavity}
\vn{%param%n_part} is the number of particles in a bunch and is used
by \vn{BeamBeam} element to determine the strength of the beambeam
interaction. \vn{%param%n_part} is also used by \vn{Lcavity} elements
for wake field calculations.

\index{lat_param_struct!%lost}
\index{lat_param_struct!%end_lost_at}
\index{lat_param_struct!%aperture_limit_on}
When tracking particles through a lattice the variable
\vn{%param%aperture_limit_on} determines if apertures are checked.
\vn{%param%lost} is used to signal if a particle is lost and
\vn{%param%ix_lost} gives the index of the element at which a particle
is lost. Additionally, \vn{%param%end_lost_at} is used to indicate at
which end the particle was lost at. See Chapter~\ref{c:tracking} for
more details.

\index{lat_param_struct!%t1_with_RF}
\index{lat_param_struct!%t1_no_RF}
\index{lat_param_struct!%stable}
\vn{%param%t1_with_RF} and \vn{%param%t1_no_RF} are the 1--turn transfer
matrices from the start of the lattice to the end. \vn{%param%t1_with_RF}
is the full transfer matrix with RF on. \vn{%param%t1_no_RF} is the
transverse transfer matrix with RF off. \vn{%param%t1_no_RF} is used to
compute the Twiss parameters. When computing the Twiss parameters
\vn{%param%stable} is set according to whether the matrix is stable or
not. If the matrix is not stable the Twiss parameters cannot be
computed. If unstable, \vn{%param%growth_rate} will be set to the
growth rate per turn of the unstable mode.  \vn{%param%t1_with_RF} and
\vn{%param%t1_no_RF} are set by various routines. Other routines use
these matrices as input for calculations.

%----------------------------------------------------------------------------
\section{Bunch_start Component}
\label{s:lat_beam_start}

The \vn{%beam_start} component within the \vn{lat_struct} is a
\vn{coord_struct} structure for holding the information obtained from
\vn{beam_start} statements (\sref{s:beam_start}) in a \bmad lattice
file. 

This component is not used in any standard \bmad calculation. It is up
to an individual program to use as desired.

%----------------------------------------------------------------------------
\section{Finding Elements and Changing Their Value}
\label{s:lat_ele_change}

\bmad has two routines for searching for an element in a lattice:
\begin{example}
  \vnr{element_locator}(ele_name, lat, ix_ele) ! locate by name
  \vnr{elements_locator}(class, lat, indx)        ! locate by class
\end{example}
\vn{element_locator} searches the \vn{lat%ele(:)} array for an
element of the correct name. Alternatively, \vn{elements_locator}
searches for all elements of a given class and returns an array of
indexes. To see how simple searching a lattice is consider the heart
of \vn{element_locator}:
\begin{example}
  do ix_ele = 0, lat%n_ele_max
    if (lat%ele(ix_ele)%name == ele_name) return
  enddo
\end{example}

Once an element or elements are identified in the lattice,
changing an element attribute generally involves changing values in the 
\vn{lat%ele(i)%value(:)} array. This is done using the 
\vnr{set_ele_attribute} routine. For example:
\begin{example}
  type (lat_struct) lat
  logical err_flag, make_xfer_mat
  ...
  call element_locator ('Q01W', lat, ix_ele)
  call set_ele_attribute (lat, ix_ele, 'K1', 0.1_rp, err_flag, make_xfer_mat)
\end{example}
\index{Overlay}
This example sets the \vn{K1} attribute of an element named \vn{Q01W}.
\vn{set_ele_attribute} checks whether this element is actually free to
be varied and sets the \vn{err_flag} logical accordingly. An element's
attribute may not be freely varied if, for example, the attribute is
controlled via an \vn{Overlay}.

\index{bmad_common_struct!auto_bookkeeper}
There is a global variable \vn{bmad_com%auto_bookkeeper} that 
controls whether bookkeeping routines are called by routines that do tracking
and routines that calculate transfer matrices. 
This is done to make sure that the control 
and attribute information in a lattice is
up--to--date. Since this bookkeeping needs only be done when element attributes
are changed, this can lead to a significant waste of time. Setting 
\vn{bmad_com%auto_bookkeeper} to \vn{.false.} turns off this
automatic bookkeeping.
As a result, if an attribute value is changed directly the appropriate bookkeeping
routines need to be called:
\begin{example}
  type (lat_struct) lat
  bmad_com%auto_bookkeeper = .false.
  lat%ele(ix_ele)%value(k1$) = 0.1
  call control_bookkeeper (lat, ix_ele)
\end{example}
Alternatively, if multiple elements are modified, then \vn{lattice_bookkeeper} can be
used to do bookkeeping for the entire lattice:
\begin{example}
  type (lat_struct) lat
  ... set some attributes ...
  call lattice_bookkeeper (lat)
\end{example}

%----------------------------------------------------------------------------
\section{Elements Controlling Other Elements}
\label{s:lat_control}

\index{Routine!get_element_lord_list}
\index{Routine!get_element_slave_list}
Generally anyone using \bmad routines will not need to bother with the
details of how elements control other elements
(\sref{s:lat_ele_change}).  It is always safer to use the bookkeeping
routines provided by \bmad rather than directly manipulating the
control information.  To simply find the slaves and lords of a given
element use the routines \vn{get_element_lord_list} and
\vn{get_element_slave_list}.  The following is thus meant for those
unusual situations where knowledge of how \bmad implements control is
needed.


The element control information is stored in the \vn{%control(:)} array, 
Each element of this array is a \vn{control_struct} structure 
\begin{example}
  type control_struct
    real(rp) coef                ! control coefficient
    integer ix_lord                ! index to lord element
    integer ix_slave               ! index to slave element
    integer ix_attrib              ! index of attribute controlled
  end type
\end{example}
\index{Girder}
\index{Overlay}
\index{Group}
\vn{%ix_lord} and \vn{%ix_slave} give the indices in the \vn{%ele(:)}
array of a lord element and an element it controls. A lord element
\vn{%ele(i_lord)} has assigned to it a block \vn{%control(:)}
elements.  The following example prints the names and controlled
attributes of the slaves of a particular lord element. If the lord is
an \vn{Overlay} or \vn{Group} then \vn{%control(:)%ix_attrib} and 
\vn{%control(:)%coef} give the attribute index of the controlled
attribute and the appropriate  coefficient 
\begin{example}
  type (lat_struct) lattice
  ...
  ix1 = lattice%ele(i_lord)%ix1_slave  ! start of block
  ix2 = lattice%ele(i_lord)%ix2_slave  ! end of block
  print *, 'Slaves for lord: ', lattice%ele(i_lord)%name
  do i = ix1, ix2
    i_slave = lattice%control(i)%ix_slave
    if (lattice%ele(i_lord)%control_type == super_lord$) then
      print *, '  ', i, '  ', lattice%ele(i_slave)%name
    else   ! must be overlay or slave
      ixa = lattice%control(i)%ix_attrib
      attrib_name = attribute_name (lattice%ele(i_slave), ixa)
      print *, '  ', i, '  ', lattice%ele(i_slave)%name, attrib_name
    endif
  enddo
\end{example}

Going backward from a slave to its lords goes through one level of
indirection using the \vn{%ic_} array as shown in the next example
\begin{example}
  ix1 = lattice%ele(i_slave)%ic1_lord
  ix2 = lattice%ele(i_slave)%ic2_lord
  print *, 'Lords for slave: ', lattice%ele(i_slave)%name
  do i = ix1, ix2
    ix = lattice%ic(i)
    i_lord = lattice%control(ix)%ix_lord
    print *, '  ', i, '  ', lattice%ele(i_lord)%name
  enddo
\end{example}
\index{Group}
For historical reasons, since a \vn{Group} element only makes changes
in the values of the attributes it controls, \vn{Group} elements are
not included in the list of lords generated by the above example.

%----------------------------------------------------------------------------
\section{Pointers}
\label{s:lat:point}
\index{Lat_struct!pointers}

Since the \vn{lat_struct} has pointers within it there is an extra burden on
the programmer to make sure that allocation and deallocation is done
properly. To this end the equal sign has been overloaded by the
routine \vn{lat_equal_lat} so that when one writes
\begin{example}
    type (lat_struct) lattice1, lattice2
    ! ... some calculations ...
    lattice1 = lattice2
\end{example}
the pointers in the \vn{lat_structs} will be handled properly. The
result will be that lattice1 will hold the same information as
\vn{lattice2} but the pointers in \vn{lattice1} will point to
different locations in physical memory so that changes to one lattice
will not affect the other.

Initial allocation of the pointers in a \vn{lat_struct} variable is
generally handled by the \vnr{bmad_parser} and \vn{lat_equal_lat}
routines.  Once allocated, local \vn{lat_struct} variables must have
the save attribute or the pointers within must be appropriately
deallocated before leaving the routine.
\begin{example}
  type (lat_struct), save :: lattice     ! Either do this at the start or ...
  ...
  call deallocate_lat_pointers (lattice) ! ... Do this at the end.
\end{example}
Using the save attribute will generally be faster but will use more
memory. Typically using the save attribute will be the best choice.
