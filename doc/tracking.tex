\chapter{Tracking and Transfer Maps}
\label{c:tracking}
\index{Tracking}

\bmad can do two types of tracking. One type uses a single particle
and tracks its coordinates throughout the lattice. The other type
takes a beam distribution and tracks the centroids and sigmas of
``macroparticles''. Discussion of macroparticle tracking will be
deferred until the last section of this chapter.

%----------------------------------------------------------------
\section{The coord_struct}
\index{Coord_struct}

For single particle tracking the starting point is the
\vn{coord_struct} whose definition is 
\begin{example}
  type coord_struct
    real(rp) vec(6)     ! (x, p_x, y, p_y, z, p_z)
    complex(rp) spin(2) ! particle spin in spinor notation
  end type
\end{example}
The \vn{coord_struct} defines the phase
space vector of the particle at a certain longitudinal location.

\index{Lat_struct!%n_ele_max}
To get an orbit, that is, the particle position at every element in a
lattice, you will need an array of \vn{coord_struct}s. Since the
number of elements in the lattice is not known in advance the array
must be declared to be allocatable.
\begin{example}
  type (coord_struct), allocatable :: orbit(:)
\end{example}
An example of how to do multi-turn tracking (assuming a circular lattice) is
\begin{example}
  type (lat_struct) lat             ! lattice to track through
  type (coord_struct), allocatable :: orbit(:)
  ...
  call bmad_parser ('this_lattice', lat)
  ...
  call reallocate_coords (orbit, lat%n_ele_max)
  orbits(0)%vec = (/ 0.01, 0.2, 0.3, 0.4, 0.0, 0.0 /) ! initialization
  do i = 1, n_turns
    call track_all (lat, orbit)
    orbit(0) = orbit(lat%n_ele_track)
  end do
\end{example}
\index{Super_lord}
\vn{orbit(n)} holds the particle's position at the exit end of the
$n$\Th element. Since \vn{super_lord} (\sref{s:control.type}) elements
have an associated particle position, the upper bound of the
\vn{orbit(:)} array must be at least \vn{lat%n_ele_max}
(\sref{s:lat.struct}).  The call to \vn{reallocate_coords} does the
allocation for the \vn{orbit(:)} array. Since \vn{lat%ele(0)} is
essentially a marker element \vn{orbit(0)} is the orbit at the start
of the lattice.  \vn{track_all} takes \vn{orbit(0)} and tracks through
the list of lattice elements until it gets to the last trackable
element \vn{lat%n_ele_track} (\sref{s:lat.struct}).

If you are writing a routine where the \vn{coord_struct} array is
local (not passed as an argument to the routine) then you have to
decide how to cleanup the allocated \vn{coord_struct} memory at the
end of the routine. In general you have two choices: 1) Deallocate
the array. This is the cleanest solution but it can be slow since you
have to allocate afresh each time the routine is called. 2) Use the
save attribute so that the array stays around until the next time the
routine is called 
\begin{example}
  type (coord_struct), allocatable, save :: orb(:) 
\end{example}
Saving the \vn{coord_stuct} is faster but leaves memory tied up. 

%----------------------------------------------------------------
\section{Tracking Through the Elements}

The routine \vnr{track1} is the routine that tracks through one
element in the lattice. The routine \vnr{track_all} calls \vn{track1}
in a loop over all elements to track through the entire
lattice. Alternatively the routine \vnr{track_many} can be used to
track through a selective number of elements or to track backwards
(See \sref{s:reverse.track}). The routines used for tracking
and closed orbit calculations are listed in Section \sref{r:track}.

\index{lat_param_struct!lost}
\index{lat_param_struct!ix_lost}
\index{lat_param_struct!plane_lost_at}
\index{lat_param_struct!end_lost_at}
\index{Tracking!example}
The \vn{track_all} routine serves as a good example of how tracking
works. \vn{track_all} tracks a particle through a lattice from
beginning to end. Its code, condensed slightly, is shown in
Figure~\ref{f:track.all}.  The \vn{reallocate_coord} call (line~13) is
done in case the number of elements in the lattice has changed. The
call to \vn{track1} (line~18) tracks through one element from the exit
end of the $n-1$\St\ element to the exit end of the $n$\Th
particle. \vn{lat%param%lost} is a logical that signals the
calling routine whether a particle has been lost.  This generally
happens when the particle's position is larger then the aperture. When
a particle is lost \vn{lat%param%ix_lost} is used to record in
what element the loss occurred.

\begin{figure}[htb]
\begin{centering}
\small
\begin{listing}{1}
  subroutine track_all (lat, orbit)
    use bmad_struct
    use bmad_interface
    implicit none
    type (lat_struct)  lat
    type (coord_struct), allocatable :: orbit(:)
    integer n

  ! Init

    lat%param%lost = .false.
    if (size(orbit) < lat%n_ele_max+1) &
                    call reallocate_coord (orbit, lat%n_ele_max)

  ! Track through the elements and check for lost particles.

    do n = 1, lat%n_ele_track
      call track1 (orbit(n-1), lat%ele(n), lat%param, orbit(n))
      if (lat%param%lost) then
        lat%param%ix_lost = n
        return
      endif
    enddo
  end subroutine
\end{listing}
\caption{Condensed track_all code.}
\end{centering}
\label{f:track.all}
\end{figure}

%----------------------------------------------------------------
\section{Closed Orbit}
\index{Closed orbit}

For a circular lattice the closed orbit may be calculated using
\vn{closed_orbit_calc}. By default this routine will track in the
forward direction which is acceptable unless the particle you are
trying to simulate is traveling in the reverse direction and there is
radiation damping on. In this case you must tell
\vn{closed_orbit_calc} to do backward tracking. This routine works by
iteratively converging on the closed orbit using the 1--turn matrix to
calculate the next guess. On rare occasions if the nonlinearities are
strong enough, this can fail to converge. An alternative routine is
\vn{closed_orbit_from_tracking} which tries to do things in a more
robust way but with a large speed penalty.

%----------------------------------------------------------------
\section{Apertures}
\index{Tracking!apertures}

\index{lat_param_struct!aperture_limit_on}
\index{bmad_common_struct!max_aperture_limit}
The logical \vn{lat%param%aperture_limit_on} determines if element
apertures (See \sref{s:limit}) are used to determine if a
particle has been lost in tracking.  The default
\vn{lat%param%aperture_limit_on} is True.  Even if this is False
there is a ``hard'' aperture limit set by
\vn{bmad_com%max_aperture_limit}. This hard limit is used to prevent
floating point overflows. The default hard aperture limit is 1000
meters. Additionally, even if a particle is within the hard limit,
some routines will mark a particle as lost if the tracking calculation
will result in an overflow.

\index{lat_param_struct!end_lost_at}
\index{lat_param_struct!lost}
\index{lat_param_struct!ix_lost}
\index{Entrance_end}
\index{Exit_end}
\vn{lat%param%lost} is the logical to check to see if a particle has
been lost. \vn{lat%param%ix_lost} is set by \vn{track_all} and gives
the index of the element at which a particle is lost.
\vn{%param%end_lost_at} gives which end the particle was lost at. 
The possible values for \vn{lat%param%end_lost_at} are:
\begin{example}
  entrance_end\$
  exit_end\$
\end{example}
When tracking forward, if a particle is lost at the exit end of an
element then the place where the orbit was outside the aperture is at
\vn{orbit(ix)} where \vn{ix} is the index of the element where the
particle is lost (given by \vn{lat%param%ix_lost}). If the
particle is lost at the entrance end then the appropriate index is one
less (remember that \vn{orbit(i)} is the orbit at the exit end of an
element). To sort this out and to determine in what plane the particle
is lost in use the routine \vnr{lost_particle_info}.

To tell how a particle is lost, check the \vn{lat%param%plane_lost_at}
parameter. Possible values for this are:
\begin{example}
  x_plane\$
  y_plane\$
  z_plane\$
\end{example}
\vn{x_plane\$} and \vn{y_plane\$} indicate that the particle was lost
either horizontally, or vertically. \vn{z_plane\$} indicates that the
particle was turned around in an \vn{lcavity} element. That is, the 
cavity was deaccelerating the particle and the particle did not not have
enough energy going into the cavity to make it to the exit.

%----------------------------------------------------------------
\section {Tracking Methods}

\index{Ele_struct!%tracking_method}
For each element the method of tracking may be set either via the
input lattice file (see \sref{s:tkm}) or directly in the
program by setting the \vn{%tracking_method} attribute of an element
\begin{verbatim}
  type (ele_struct) ele
  ...
  ele%tracking_method = boris$  ! for boris tracking
\end{verbatim}
To form the corresponding parameter to a given tracking method just
put ``\$'' after the name. For example, the \vn{bmad_standard}
tracking method is specified by the \vn{bmad_standard\$}
parameter.

\index{Ele_struct!%mat6}
It should be noted that except for \vni{Linear} tracking, none of the
\bmad tracking routines make use of the \vn{ele%mat6} transfer
matrix. The reverse, however, is not true.  The transfer matrix
routines (\vn{lat_make_mat6}, etc.)  will do tracking.

\index{Synchrotron radiation!calculating}
\bmad simulates radiation damping and excitation by applying a kick
just before and after each element. To turn on radiation damping
and/or excitation use the \vnr{setup_radiation_tracking} routine.

%----------------------------------------------------------------
\section{Taylor Maps}
\label{s:taylor.track}
\index{Taylor Map}

A list of routines for manipulating Taylor maps is given
in~\sref{r:taylor}. The order of the Taylor maps is set in the lattice
file using the \vn{parameter} statement (\sref{s:param}). In a program
this can be overridden using the routine \vnr{set_taylor_order}. The
routine \vnr{taylor_coef} can be used to get the coefficient of any
given term.

\index{Symp_Lie_Bmad}
\index{Symp_Lie_PTC}
\index{Symp_Map}
\index{Taylor}
\index{Taylor!deallocating}
Transfer Taylor maps for an element are generated as needed when the
\vn{ele%tracking_method} or \vn{ele%mat6_calc_method} is set to
\vn{Symp_Lie_Bmad}, \vn{Symp_Lie_PTC}, \vn{Symp_Map}, or
\vn{Taylor}. Since generating a map can take an appreciable time,
\bmad follows the rule that once generated, these maps are never
regenerated unless an element attribute is changed.  To generate a
Taylor map within an element irregardless of the
\vn{ele%tracking_method} or \vn{ele%mat6_calc_method} settings use the
routine \vnr{ele_to_taylor}. This routine will kill any old Taylor map
before making any new one. To kill a Taylor map (which frees up the
memory it takes up) use the routine \vnr{kill_taylor}.

To test whether a \vn{taylor_struct} variable has an associated Taylor
map. That is, to test whether memory has been allocated for the map,
use the Fortran associated function:
\begin{example}
  type (bmad_taylor) taylor(6)
  ...
  if (associated(taylor(1)%term)) then  ! If has a map ...
    ...
\end{example}

To concatenate the Taylor maps in a set of elements the routine
\vnr{concat_taylor} can be used
\begin{example}
  type (lat_struct) lat          ! lattice
  type (taylor_struct) taylor(6)  ! taylor map
  ...
  call taylor_make_unit (taylor)  ! Make a unit map
  do i = i1+1, i2
    call concat_taylor (taylor, lat%ele(i)%taylor, taylor)
  enddo
\end{example}
The above example forms the transfer Taylor map starting at the end of
element \vn{i1} to the end of element \vn{i2}. Note: This example
assumes that all the elements have a Taylor map. The problem with
concatenating maps is that if there is a constant term in the map
``feed down'' can make the result inaccurate (\sref{s:taylor.phys}. To
get around this one can ``track'' a taylor map through an element
using symplectic integration.
\begin{example}
  type (lat_struct) lat          ! lattice
  type (taylor_struct) taylor(6)  ! taylor map
  ...
  call taylor_make_unit (taylor)  ! Make a unit map
  do i = i1+1, i2
    call call taylor_propagate1 (taylor, lat%ele(i), lat%param)
  enddo
\end{example}
\index{Ds_step}
\index{Integrator_order}
Symplectic integration is typically much slower than concatenation.
The width of an integration step is given by \vn{%ele%value(ds_step\$}.
The attribute \vn{%ele%value(num_steps\$)}, which gives the number
of integration steps, is a dependent variable 
(\sref{s:depend}) and should not be set directly.
The order of the integrator (\sref{s:taylor.phys})
is given by \vn{%ele%integrator_order}. 
PTC (\sref{c:etienne}) currently implements integrators of order 2, 4, or 6.

%----------------------------------------------------------------
\section{Macroparticle Tracking}
\index{Macroparticles!tracking}
\index{Tracking!Macroparticles}
\label{s:macro.track}

\begin{figure}[!tb]
\small
\begin{listing}{1}
program macroparticle_test

  use bmad
  use macroparticle_mod

  implicit none

  type (lat_struct) lat
  type (macro_beam_struct) beam
  type (macroparticle_struct) mp
  type (macro_init_struct) init
  type (macro_bunch_params_struct) params

  integer i, j, k, n

  namelist / beam_init / init

!

  bmad_com%use_liar_lcavity = .true.
  call bmad_parser ('test.bmad', lat)

  open (1, file = 'test.init', status = 'old')
  init%E_0 = lat%ele(0)%value(E_tot$)
  read (1, nml = beam_init)
  close (1)

  call init_macro_distribution (beam, init, .true.)
  call track_macro_beam (lat, beam)

  print *
  print '(13x, a)', "x          x'    sig(1,1)    sig(1,2)    sig(2,2)"
  do i = 1, size(beam%bunch)
    do j = 1, size(beam%bunch(i)%slice)
      do k = 1, size(beam%bunch(i)%slice(j)%macro)
        mp = beam%bunch(i)%slice(j)%macro(k)
        call mp_to_angle_coords (mp, lat%ele(n)%value(E_tot$))
        print '(3i2, 1p, 5e12.4)', i, j, k, mp%r%vec(1), mp%r%vec(2), &
                  mp%sigma(s11$), mp%sigma(s12$), mp%sigma(s22$)
      enddo
    enddo
  enddo

  call calc_macro_bunch_params (beam%bunch(1), lat%ele(lat%n_ele_track), &
                                params)

  print *
  print '(a, e12.4)', "Normalized y emittance at end of lattice: ", params%y%emitt

end program
\end{listing}
\caption{Example program showing macroparticle tracking.}
\label{f:macro.program}
\end{figure}

Macroparticles are particles that also have an associated size. 
See \sref{s:macro} for more details. Note: Macroparticles where implemented 
into \bmad when bunch tracking was first introduced. Macroparticles have the
theoretical advantage over particle tracking since the number of macroparticles
needed to characterize a bunch is smaller. However, transport of the macroparticle
sigma matrix adds complexity and recent development in \bmad in terms of tracking
bunches have generally focused on straight particle tracking (\sref{s:part.track}).

The definition of the \vn{macro_struct} structure that defines a
macroparticle is shown in Figure~\ref{f:macro.struct}.  
\begin{figure}[htb]
\centering
\small
\begin{verbatim}
  type macro_struct
    type (coord_struct) r   ! Center of the macroparticle
    real(rp) sigma(21)      ! Sigma matrix.
    real(rp) :: sig_z = 0   ! longitudinal macroparticle length.
    real(rp) k_loss         ! loss factor (V/m). scratch variable for tracking.
    real(rp) charge         ! charge in a macroparticle (Coul).
    integer :: iz = 0       ! index to ordering of particles in z.
  end type
\end{verbatim}
\caption{The \vn{macro_struct}\ that defines a macroparticle.}
\label{f:macro.struct}
\end{figure}

\vn{%r} is the
coordinates of the macroparticle center. \vn{%sigma(:)} holds the
sigma matrix. Since the $6 \times 6$ sigma matrix is symmetric only 21
elements need to be stored. Symbolic constants \vn{s$ij$\$} ($i$, $j$
= $1, \ldots, 6$) are defined to map to between the linear
\vn{%sigma(:)} array and the matrix. Example
\begin{example}
  type (macro_struct) macro
  sigma_z = sqrt(macro%sigma(s55\$))
\end{example}
To convert \vn{%sigma(:)} into an actual matrix use the
\vn{mp_sigma_to_mat} routine.

\index{LIAR}
\bmad uses canonical phase space
coordinates(See \sref{s:phase.space.coords}). The LIAR program uses
``angle'' phase space coordinates
\begin{equation}
  (x, x', y, y', -z, E)
\end{equation}
The notation can be confusing here since in LIAR's notation positive
$z$ is in the opposite direction to the \bmad convention. Thus in
LIAR's notation the LIAR phase space coordinates are
\begin{equation}
  (x, x', y, y', z, E)
\end{equation}
To convert between \bmad and LIAR coordinates there are two routines
called \vn{mp_to_angle_coords} and \vn{mp_to_canonical_coords}.


Macroparticles are grouped into slices, an array of slices make up a
bunch, and a number of bunches make up a beam. The routine
\vn{init_macro_distribution} can be used to initialize a
\vn{macro_beam_struct} structure. \vn{init_macro_distribution}
implements the LIAR initialization algorithm (See
\sref{s:macro}). Once a beam is initialized, tracking can be performed
using \vn{track_macro_beam}. An example program is shown in
figure~\ref{f:macro.program}.

The equal sign in the assignments
\begin{example}
  beam1 = beam2
  bunch1 = bunch2
  slice1 = slice2
\end{example}
is overloaded by the routines \vn{beam_equal_beam}, \vn{bunch_equal_bunch} and
\vn{slice_equal_slice} to ensure that the pointers of \vn{beam1}, \vn{bunch1}
and \vn{slice1} do not point to the same memory locations as the pointers of 
\vn{beam2}, \vn{bunch2} and \vn{slice2}.

The \vn{init} variable (line 11) holds the information to initialize
the macroparticle distribution. The values of the components in the
\vn{init} variable are set using a namelist read (line 24). An example
of an input file that can be used to set \vn{init} is shown in
figure~\ref{f:beam.init}. Line 23 sets the initial reference energy of
the beam to be the reference energy at the start of the linac so this
does not have to be set in the input file.  The
\vn{init_macro_distribution} routine (line 27) initializes the
\vn{beam} variable. The \vn{beam} variable is a container that holds
the macroparticles that make up the beam. The \vn{track_macro_beam} routine
tracks these macroparticles through to the end of the linac. If it is
desired to track only through part of the linac then \vn{track_macro_beam}
has optional arguments that allow this. If it is desired to only track through 1
element (similarly to \vn{track1}) then the subroutine \vn{track1_beam} is also
available. Lines 30 through 41 print out
some horizontal parameters from the position at the end of the linac.

The subroutine \vn{calc_macro_bunch_params} will find the bunch
centroid, emittances, sigmas, twiss parameters and energy-Z
correlation.

\begin{figure}
\begin{listing}{1}
  &beam_init
    init%x%beta      = 4
    init%x%alpha     = 0.16
    init%x%norm_emit = 4.d-6  ! normalized emittance
    init%y%beta      = 16
    init%y%alpha     = 4
    init%y%norm_emit = 1.0d-8 ! normalized emittance
    init%center      = -5e-3, 1.0e-4, 1.0e-3, 0e-6, 0.0, 0.0
    init%sig_e       = 10e-3
    init%sig_z       = 10e-6
    init%sig_e_cut   = 3
    init%sig_z_cut   = 3
    init%n_bunch     = 1
    init%n_slice     = 2
    init%n_macro     = 1
    init%n_part      = 1e12
  /
\end{listing}
\caption{Example beam initialization file.}
\label{f:beam.init}
\end{figure}

\break

%----------------------------------------------------------------
\section{Reverse Tracking}
\label{s:reverse.track}
\index{Tracking!reverse}

There are two ways to do reverse tracking in which the particle goes
in the direction of decreasing \vn{s}. The first way is to use the
\vnr{track_many} routine. See the \vn{track_many} routine for more
details. The advantage of using \vn{track_many} is that it is
simple. The disadvantage is that it can slow things down some since
each element goes through a reversal process every time it is tracked
through. If a program is doing a lot of tracking the other option
is to form a reversed lattice with the elements in the reverse order
and track through that. The routine \vnr{lat_reverse} will do
this. One must be somewhat careful since the reversed lattice uses a
reversed coordinate system. The transformation between the reversed
and unreversed lattices is
\Begineq
  (x, p_x, y, p_y, z, p_z) -> (x, -p_x, y, -p_y, -z, p_z)
\Endeq
See the \vn{lat_reverse} routine for more details.

Generally tracking backwards is simply the reverse of tracking
forwards (time reversal symmetry). That is, if you start at some
place, track forward for some distance and then track back to the
starting place the ending orbit will be equal to the starting
orbit. However, it should always be kept in mind that radiation
damping or excitation breaks this symmetry.

%----------------------------------------------------------------
\section{Particle Distribution Tracking}
\label{s:part.track}
\index{Tracking!particle distributions}

Initializing a distribution of particles to conform to some initial set of
Twiss parameters and emittances can be done using the routine
\vnr{init_beam_distribution}. Tracking is then performed similarly to
macroparticle tracking.

%----------------------------------------------------------------
\section{Spin Tracking}
\label{s:spin.track}
\index{Tracking!spin}

Spin tracking has been implemented for \vn{bmad_standard}, \vn{boris} and
\vn{adaptive_boris} tracking methods. To turn spin tracking on use the
\vn{bmad_com%spin_tracking_on} flag. Then, after properly initializing the spin
in the \vn{coord_struct}, calls to \vn{track1} will track both the
particle orbit and the spin.

